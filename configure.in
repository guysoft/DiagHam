dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/config.h)
AM_INIT_AUTOMAKE(DiagHam,0.01)

AM_CONFIG_HEADER(src/config_ac.h:src/config_ac.h.in)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_RANLIB
AC_PROG_INSTALL

dnl Checks for libraries.
AC_CHECK_LIB(m, cos,MATH_LIBS="-lm",AC_MSG_WARN(math lib not found))
AC_CHECK_LIB(pthread, pthread_create,THREAD_LIBS="-lpthread",AC_MSG_WARN(pthread lib not found : can't use SMP))

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/time.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl parse mpi options

AC_ARG_ENABLE(mpi, [AC_HELP_STRING([--enable-mpi],[enable MPI support])], [HAVE_PKG_MPI=$enableval],[HAVE_PKG_MPI=no])
AC_ARG_WITH(mpi-cxx, [AC_HELP_STRING([--with-mpi-cxx], [use MPI C++ compiler (enables MPI) @<:@mpiCC@:>@])],
[HAVE_PKG_MPI=yes 
if test X${withval} = Xyes; then
  MPI_CXX=mpiCC
else
  MPI_CXX=${withval}
fi
AC_MSG_CHECKING(user-defined MPI C++ compiler)
AC_MSG_RESULT([${MPI_CXX}])
]
)

AC_ARG_WITH(mpi-libs,[AC_HELP_STRING([--with-mpi-libs="LIBS"],[MPI libraries @<:@"-lmpi"@:>@])],
[MPI_LIBS=${withval}
AC_MSG_CHECKING(user-defined MPI libraries)
AC_MSG_RESULT([${MPI_LIBS}])
]
)


AC_ARG_WITH(mpi-incdir,[AC_HELP_STRING([--with-mpi-incdir=DIR],[MPI include directory @<:@MPIROOT/include@:>@])],
[
MPI_INC=${withval}
AC_MSG_CHECKING(user-defined MPI includes)
AC_MSG_RESULT([${MPI_INC}])
]
)


AC_ARG_WITH(mpi-libdir,
[AC_HELP_STRING([--with-mpi-libdir=DIR],[MPI library directory @<:@MPIROOT/lib@:>@])],
[
MPI_LIBDIR=${withval}
AC_MSG_CHECKING(user-defined MPI library directory)
AC_MSG_RESULT([${MPI_LIBDIR}])
]
)

AC_MSG_CHECKING(whether we are using MPI)
AC_MSG_RESULT([${HAVE_PKG_MPI}])
 
if test X${HAVE_PKG_MPI} = Xyes; then
  AC_DEFINE(HAVE_MPI,,[define if we are using MPI])
fi

AM_CONDITIONAL(BUILD_MPI, test X${HAVE_PKG_MPI} = Xyes)

dnl check for MPI c++ compiler

if test X${HAVE_PKG_MPI} = Xyes; then

  if test -z "${MPI_DIR}" &&  test -z "${MPI_CXX}"; then
    MPI_CXX=mpiCC
  fi
fi

if test -n "${MPI_CXX}"; then

  if test -f ${MPI_CXX}; then   
    MPI_CXX_EXISTS=yes
  else
    AC_CHECK_PROG(MPI_CXX_EXISTS, ${MPI_CXX}, yes, no)
  fi

  if test "X${MPI_CXX_EXISTS}" = "Xyes"; then
    CXX=${MPI_CXX}
  else
    AC_MSG_ERROR([MPI C++ compiler (${MPI_CXX}) not found.])
  fi

fi

dnl Checks for library functions.

dnl CXXFLAGS="-O3 -ffast-math -g -Wall  -march=athlon"
CXXFLAGS="-O2 -g -Wall "
INCLUDES="-I\$(top_srcdir)/src "
AC_SUBST(CXXFLAGS)
AC_SUBST(INCLUDES)
AC_SUBST(MATH_LIBS)
AC_SUBST(THREAD_LIBS)

AC_OUTPUT(Makefile src/Makefile src/Programs/Makefile src/Programs/QHE/Makefile src/Programs/QuantumDot/Makefile src/Programs/Spin/Makefile src/Architecture/Makefile src/Architecture/ArchitectureOperation/Makefile src/FunctionBasis/Makefile src/FunctionBasis/QHEFunctionBasis/Makefile src/Tensor/Makefile src/QuantumNumber/Makefile src/Interaction/Makefile src/Interaction/InternalInteraction/Makefile src/HilbertSpace/Makefile src/HilbertSpace/QHEHilbertSpace/Makefile src/HilbertSpace/SpinHilbertSpace/Makefile src/HilbertSpace/DMRGHilbertSpace/Makefile src/HilbertSpace/QuantumDotHilbertSpace/Makefile src/HilbertSpace/ManyBodyHilbertSpace/Makefile src/Output/Makefile src/GeneralTools/Makefile src/Vector/Makefile src/Polynomial/Makefile src/Matrix/Makefile src/LanczosAlgorithm/Makefile src/Hamiltonian/Makefile src/Hamiltonian/QHEHamiltonian/Makefile src/Hamiltonian/QuantumDotHamiltonian/Makefile src/Hamiltonian/SpinHamiltonian/Makefile src/Hamiltonian/DMRGHamiltonian/Makefile src/TensorProduct/Makefile src/DMRGAlgorithm/Makefile src/Operator/Makefile src/MathTools/Makefile src/Color/Makefile src/BitmapPicture/Makefile src/Options/Makefile src/Tools/Makefile src/Tools/QuantumDot/Makefile src/Tools/QuantumDot/Potential/Makefile src/Tools/QuantumDot/Spectra/Makefile)
